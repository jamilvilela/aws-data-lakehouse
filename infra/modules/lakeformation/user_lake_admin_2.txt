data "aws_iam_user" "lake_admin" {
  user_name = "lake-admin"
}

resource "aws_iam_user_policy_attachment" "lake_admin_managed" {
  for_each = toset([
    "arn:aws:iam::aws:policy/AWSLakeFormationDataAdmin",
    "arn:aws:iam::aws:policy/AWSGlueConsoleFullAccess",
    "arn:aws:iam::aws:policy/CloudWatchLogsReadOnlyAccess",
    "arn:aws:iam::aws:policy/AWSLakeFormationCrossAccountManager",
    "arn:aws:iam::aws:policy/AmazonAthenaFullAccess",
    "arn:aws:iam::aws:policy/AWSCloudFormationReadOnlyAccess"
  ])
  user       = data.aws_iam_user.lake_admin.user_name
  policy_arn = each.value
}

resource "aws_iam_user_policy" "lake_admin_inline" {
  name = "LakeFormationSLR"
  user = data.aws_iam_user.lake_admin.user_name
  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect = "Allow"
        Action = "iam:CreateServiceLinkedRole"
        Resource = "*"
        Condition = {
          StringEquals = {
            "iam:AWSServiceName" = "lakeformation.amazonaws.com"
          }
        }
      },
      {
        Effect = "Allow"
        Action = "iam:PutRolePolicy"
        Resource = "arn:aws:iam::${data.aws_caller_identity.current.account_id}:role/aws-service-role/lakeformation.amazonaws.com/AWSServiceRoleForLakeFormationDataAccess"
      }
    ]
  })
}

resource "aws_iam_user_policy_attachment" "lake_admin_lf_user_pass_role" {
  user       = data.aws_iam_user.lake_admin.user_name
  policy_arn = aws_iam_policy.lf_user_pass_role_policy.arn
}

resource "aws_iam_user_policy_attachment" "lake_admin_ram_access" {
  user       = data.aws_iam_user.lake_admin.user_name
  policy_arn = aws_iam_policy.lf_ram_access_policy.arn
}


resource "aws_iam_user_policy" "lake_admin_basic" {
  name = "DatalakeUserBasic"
  user = data.aws_iam_user.lake_admin.user_name
  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect = "Allow"
        Action = [
          "lakeformation:GetDataAccess",
          "glue:GetDatabase",
          "glue:GetDatabases",
          "glue:GetTable",
          "glue:GetTables",
          "glue:GetTableVersions",
          "glue:SearchTables",
          "glue:UpdateTable",
          "glue:GetPartitions",
          "lakeformation:GetResourceLFTags",
          "lakeformation:ListLFTags",
          "lakeformation:GetLFTag",
          "lakeformation:SearchTablesByLFTag",
          "lakeformation:SearchDatabasesByLFTags"
        ]
        Resource = "*"
      }
    ]
  })
}

resource "aws_iam_user_policy_attachment" "lake_admin_athena_access" {
  user       = data.aws_iam_user.lake_admin.user_name
  policy_arn = "arn:aws:iam::aws:policy/AmazonAthenaFullAccess"
}

resource "aws_iam_user_policy_attachment" "lake_admin_governed_table" {
  user       = data.aws_iam_user.lake_admin.user_name
  policy_arn = aws_iam_policy.lf_governed_table_policy.arn
}